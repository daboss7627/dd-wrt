# This file is part of pound testsuite. -*- autotest -*-
# Copyright (C) 2022 Sergey Poznyakoff
#
# Pound is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 3 of the License, or
# (at your option) any later version.
#
# Pound is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with pound.  If not, see <http://www.gnu.org/licenses/>.
m4_version_prereq([2.52g])

## PT_CONF(CONFIG,[STATUS = `0'], [STDOUT], [STDERR], [RUN-IF-FAIL], [RUN-IF-PASS])
m4_define([PT_CONF],
[cat > pound.cfg <<_CONF
$1
_CONF
AT_CHECK([pound -c -Wno-dns -f pound.cfg],m4_shift($@))])

m4_define([PT_PREREQ_PERL],[perl -v >/dev/null 2>&1 || exit 77])

## PT_CHECK(CONFIG, SCRIPT)
m4_define([PT_CHECK],
[AT_CHECK([
PT_PREREQ_PERL
AT_DATA([pound.cfi],
[m4_if([$1],[],[ListenHTTP
	Service
                Backend
                        Address
                        Port
                End
	End
End],[$1])
])
AT_DATA([input],
[$2
])
perl $HARNESS --transcript xscript.log input
],
m4_shift2($@))])

AT_TESTED([pound])
AT_INIT
m4_include([config.at])
m4_include([echo.at])
m4_include([xhttp.at])
m4_include([checkurl.at])
m4_include([errfile.at])
m4_include([maxrequest.at])
m4_include([headrem.at])
m4_include([addheader.at])
m4_include([rewriteloc.at])
m4_include([headrequire.at])
m4_include([headdeny.at])
m4_include([host.at])
m4_include([url.at])
m4_include([or.at])
m4_include([not.at])
m4_include([balancing.at])
m4_include([prio.at])
m4_include([redirect.at])
m4_include([acme.at])
AT_BANNER([Sessions])
m4_include([sessip.at])
m4_include([sessauth.at])
m4_include([sesshdr.at])
m4_include([sessparm.at])
m4_include([sessurl.at])
m4_include([sesscookie.at])
AT_BANNER([HTTPS])
m4_include([https.at])
m4_include([virthost.at])
